defaultTasks 'clean', 'todo', 'build', 'dist'

task todo {
    doLast {
        def searchTree = fileTree(dir: '.')
            .exclude('/.git/')
            .exclude('/.gradle/')
            .exclude('/.idea/')
            .exclude('**/out/')
            .exclude('**/build/')
        searchTodo(searchTree, '$' + '$$', 'not allowed for building a release')
        searchTodo(searchTree, '$' + 'todo', 'under construction, yet a release can still be built')
        searchTodo(searchTree, '$' + 'note', 'important comment')
        divider1()
    }
}

def divider1() {
    println "========================================================================================================================"
}

def divider2() {
    println "------------------------------------------------------------------------------------------------------------------------"
}

def searchTodo(tree, String marker, String help) {
    divider1()
    println "= " + marker + ": " + help
    tree.each { file ->
        def found = false
        def number = 0
        file.eachLine { line ->
            number++
            if (line.toLowerCase().contains(marker)) {
                if (!found) {
                    divider2()
                    println "+ " + file.toString()
                }
                found = true
                println "- " + number + ": " + line.toString()
            }
        }
    }
}
