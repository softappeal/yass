defaultTasks 'clean', 'todo', 'build', 'compileTutorialJava', 'createPom', 'dist'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
  mavenCentral()
}

sourceSets {
  tutorial { java.srcDir file('src/tutorial/java') }
}

dependencies {
  testCompile 'junit:junit:4.11'
  tutorialCompile sourceSets.main.output
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint"
}

jar {
  from "readme.md"
  from "license.txt"
}

task packageSources(type: Jar) {
  from "readme.md"
  from "license.txt"
  from sourceSets.main.allSource
  classifier = 'sources'
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

artifacts {
  archives(packageSources)
  archives(packageJavadoc)
}

task createPom << {
  pom {
    project {
      groupId 'ch.softappeal.' + project.name
      packaging 'jar'
      name project.name
      description 'Yet Another Service Solution'
      url 'https://github.com/softappeal/yass'
      licenses {
        license { name 'The BSD 3-Clause License' }
      }
      scm { url 'https://github.com/softappeal/yass' }
      organization { name 'softappeal GmbH Switzerland' }
      developers {
        developer { name 'Angelo Salvade' }
      }
    }
  }.writeTo("build/libs/${project.name}-${version}.pom")
}

task dist << {
  if (project.hasProperty('signing.password')) {
    def signPrefix = "build/libs/${project.name}-${version}"
    signing.sign(file("${signPrefix}.pom"))
    signing.sign(file("${signPrefix}.jar"))
    signing.sign(file("${signPrefix}-sources.jar"))
    signing.sign(file("${signPrefix}-javadoc.jar"))
  }
  ant.zip(destfile: "build/tutorial.zip") {
    fileset(dir: 'src/tutorial')
  }
  ant.zip(destfile: "build/libs.zip") {
    fileset(dir: 'build/libs')
  }
  ant.zip(destfile: "build/${project.name}-${version}_dist.zip") {
    fileset(file: 'readme.md')
    fileset(file: 'license.txt')
    fileset(file: 'build/libs.zip')
    fileset(file: 'build/tutorial.zip')
  }
}

def searchMarker(tree, divider1, divider2, title, marker) {
  println divider1
  println "= search: " + title
  tree.each { file ->
    def found = false
    def number = 0
    file.eachLine { line ->
      number++
      if (line.toLowerCase().contains(marker)) {
        if (!found) {
          println divider2
          println "+ " + file
        }
        found = true
        println "- " + number + ": " + line
      }
    }
  }
}

task todo << {
  def divider1 = "========================================================================================================================"
  def divider2 = "------------------------------------------------------------------------------------------------------------------------"
  def searchTree = fileTree(dir: '.').exclude('.git/').exclude('.gradle/').exclude('.idea/').exclude('build/').exclude('classes/')
  searchMarker(searchTree, divider1, divider2, 'abort', '$' + '$$')  // abort, not allowed for building a release
  searchMarker(searchTree, divider1, divider2, 'todo', '$' + 'todo') // under construction, yet a release can still be built
  searchMarker(searchTree, divider1, divider2, 'note', '$' + 'note') // important comment
  println divider1
}
